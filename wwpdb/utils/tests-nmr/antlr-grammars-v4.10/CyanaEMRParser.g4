/*
 CYANA EMR (Magnetic Restraint - extended for multiple chains) parser grammar for ANTLR v4.
 Copyright 2025 Masashi Yokochi

you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

parser grammar CyanaEMRParser;

options { tokenVocab=CyanaEMRLexer; }

cyana_emr:
	(
	Orientation_header |
	Tensor_header+ |
	comment |
	distance_restraints |
	torsion_angle_restraints |
	rdc_restraints |
	pcs_restraints |
	cco_restraints |
	ssbond_macro |
	hbond_macro |
	link_statement |
	stereoassign_macro |
	declare_variable |
	set_variable |
	unset_variable |
	print_macro |
	unambig_atom_name_mapping |
	ambig_atom_name_mapping
	)*
	EOF;

comment:
	COMMENT Any_name* (RETURN_CM | EOF);

/* CYANA 3.0 Reference Manual - Distance restraint file
 See also http://www.cyana.org/wiki/index.php/Distance_restraint_file
*/
distance_restraints:
	distance_restraint+;

distance_restraint:
	gen_res_num gen_simple_name gen_atom_name
	gen_res_num gen_simple_name gen_atom_name
	number number? number?
	number? number? number?;	// extensions for .cco file

/* CYANA 3.0 Reference Manual - Torsion angle restraint file
 See also http://www.cyana.org/wiki/index.php/Torsion_angle_restraint_file
*/
torsion_angle_restraints:
	torsion_angle_restraint+;

torsion_angle_restraint:
	gen_res_num gen_simple_name gen_simple_name number number number? (Type Equ_op Integer)? Or?;

/* CYANA 3.0 Reference Manual - Residual dipolar coupling restraint file
 See also http://www.cyana.org/wiki/index.php/Residual_dipolar_coupling_restraint_file
*/
rdc_restraints:
	rdc_parameter+
	(Orientation_header | comment | rdc_restraint)+;

rdc_parameter:
	Integer number number (Integer | Capital_integer | Simple_name);

rdc_restraint:
	gen_res_num gen_simple_name gen_simple_name
	gen_res_num gen_simple_name gen_simple_name
	number number number Integer number?;

/* CYANA 3.0 Reference Manual - Pseudocontact shift restraint file
 See also http://www.cyana.org/wiki/index.php/Pseudocontact_shift_restraint_file
*/
pcs_restraints:
	pcs_parameter+
	(Orientation_header | comment | pcs_restraint)+;

pcs_parameter:
	Integer number number (Integer | Capital_integer | Simple_name);

pcs_restraint:
	gen_res_num gen_simple_name gen_simple_name
	number number number Integer;

/* CYANA (undocumented) - Scalar coupling constant restraint
 See also https://www.las.jp/english/products/cyana/DyanaManual.pdf
*/
cco_restraints:
	cco_restraint+;

cco_restraint:
	gen_res_num gen_simple_name gen_simple_name gen_simple_name number number? number?
	number? number? number?;	// Karplus coefficients, A B C

/* CYANA (macro) - ssbond
*/
ssbond_macro:
	Ssbond Ssbond_resids;

/* CYANA (macro) - hbond
*/
hbond_macro:
	Hbond
		(Atom1 Equ_op_HB)? Simple_name_HB (Residue1 Equ_op_HB)? Integer_HB
		(Atom2 Equ_op_HB)? Simple_name_HB (Residue2 Equ_op_HB)? Integer_HB
	RETURN_HB;

/* CYANA (statement) - link
*/
link_statement:
	Link gen_simple_name gen_res_num gen_simple_name gen_res_num;

/* CYANA (macro) - stereoassign
*/
stereoassign_macro:
	Atom_stereo Double_quote_string RETURN_PR;

declare_variable:
	Var Simple_name_VA* RETURN_VA;

set_variable:
	SetVar;

unset_variable:
	Unset Simple_name_VA* RETURN_VA;

print_macro:
	Print Double_quote_string RETURN_PR;

/* AMBER atom nomenclature mapping generated by makeDIST_RST */
unambig_atom_name_mapping:
	Residue gen_simple_name comment?
	(mapping_list | comment)+;

mapping_list:
	Mapping Simple_name_MP Equ_op_MP Simple_name_MP+ RETURN_MP;

ambig_atom_name_mapping:
	Residue gen_simple_name comment?
	(ambig_list | comment)+;

ambig_list:
	Ambig (Simple_name_MP | Ambig_code_MP) Equ_op_MP (Simple_name_MP Integer_MP)+ RETURN_MP;

/* number expression in restrains */
number:	Float | Float_DecimalComma | Integer;

/* residue number allowing chain identification */
gen_res_num:
	Integer | Capital_integer;

/* residue name or atom name */
gen_simple_name:
	Simple_name | Capital_integer;

/* atom id extenstion with ambig code */
gen_atom_name:
	Simple_name | Capital_integer | Ambig_code;

