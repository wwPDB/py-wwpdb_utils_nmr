/*
 CYANA EMR (Magnetic Restraint - extended for multiple chains) lexer grammar for ANTLR v4.
 Copyright 2025 Masashi Yokochi

you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

lexer grammar CyanaEMRLexer;

/* Case-Insensitive Lexing
 See also https://github.com/antlr/antlr4/blob/master/doc/case-insensitive-lexing.md
*/
fragment A:		[aA]; // match either an 'a' or 'A'
fragment B:		[bB];
fragment C:		[cC];
fragment D:		[dD];
fragment E:		[eE];
fragment F:		[fF];
fragment G:		[gG];
fragment H:		[hH];
fragment I:		[iI];
fragment J:		[jJ];
fragment K:		[kK];
fragment L:		[lL];
fragment M:		[mM];
fragment N:		[nN];
fragment O:		[oO];
fragment P:		[pP];
fragment Q:		[qQ];
fragment R:		[rR];
fragment S:		[sS];
fragment T:		[tT];
fragment U:		[uU];
fragment V:		[vV];
fragment W:		[wW];
fragment X:		[xX];
fragment Y:		[yY];
fragment Z:		[zZ];

/* CYANA 3.0 Reference Manual
 See also http://www.cyana.org/wiki/index.php/CYANA_3.0_Reference_Manual
*/
Ambig_code:		(DECIMAL '-' DECIMAL ':' DECIMAL | (ALPHA | '_') SIMPLE_NAME '.' DECIMAL);
Integer:		('+' | '-')? DECIMAL;
Float:			('+' | '-')? (DECIMAL | DEC_DOT_DEC) (E ('+' | '-')? DECIMAL)?;
Float_DecimalComma:	('+' | '-')? (DEC_COM_DEC) ('E' ('+' | '-')? DECIMAL)?;
fragment DEC_DOT_DEC:	(DECIMAL '.' DECIMAL?) | ('.' DECIMAL);
fragment DEC_COM_DEC:	(DECIMAL ',' DECIMAL?) | (',' DECIMAL);
fragment DEC_DIGIT:	[0-9];
fragment DECIMAL:	DEC_DIGIT+;

Orientation_header:	'#' [ \t]* O R I E N T A T I O N [ \t]+ M A G N I T U D E [ \t]+ R H O M B I C I T Y [ \t]+ O R I [ \t]+ R E S I D U E [ \t]+ N U M B E R [ \t]* [\r\n]+;
Tensor_header:		'#' [ \t]* T E N S O R [ \t]+ M A G N I T U D E [ \t]+ R H O M B I C I T Y [ \t]+ R E S I D U E [ \t]* [\r\n]+;

SMCLN_COMMENT:		';'+ ~[\r\n]* ';'* ~[\r\n]* -> channel(HIDDEN);

COMMENT:		('#' | '!')+ -> mode(COMMENT_MODE);

/* DIANA distance restraint generated by 'qconvert' script */
NoeUpp:			N O E U P P;
NoeLow:			N O E L O W;

/* extensions for torsion angle restraints */
Type:			T Y P E;		// = Integer
Equ_op:			'=';
Or:			O R;

/* CYANA 3.0 Reference Manual - ssbond macro
 See also http://www.cyana.org/wiki/index.php/CYANA_Macro:_ssbond
*/
Ssbond:			S S B O N D;
Ssbond_resids:		DECIMAL '-' DECIMAL;

/* CYANA 3.0 Reference Manual - hbond macro
 See also http://www.cyana.org/wiki/index.php/CYANA_Macro:_hbond
*/
Hbond:			H B O N D -> pushMode(HBOND_MODE);

/* CYANA 3.0 Reference Manual - link statement
 See also http://www.cyana.org/wiki/index.php/Sequence_file
*/
Link:			L I N K;

/* CYANA 3.0 Reference Macnual - stereoassign macro
 See also http://www.cyana.org/wiki/index.php/CYANA_Macro:_stereoassign
*/
Atom_stereo:		A T O M ' '+ S T E R E O -> mode(PRINT_MODE);

Var:			V A R -> mode(VARIABLE_MODE);
Unset:			U N S E T -> mode(VARIABLE_MODE);

SetVar:			SIMPLE_NAME ':=' (SIMPLE_NAME | Float | Float_DecimalComma | Integer);

Print:			P R I N T -> mode(PRINT_MODE);

/* AMBER atom nomenclature mapping generated by makeDIST_RST */
Residue:		R E S I D U E;
Mapping:		M A P P I N G -> pushMode(MAP_MODE);
Ambig:			A M B I G -> pushMode(MAP_MODE);

Capital_integer:	[A-Z] DECIMAL;

Simple_name:		SIMPLE_NAME;
//Residue_number:	Integer;
//Residue_name:		SIMPLE_NAME;
//Atom_name:		ALPHA_NUM ATM_NAME_CHAR*;

fragment ALPHA:		[A-Za-z];
fragment ALPHA_NUM:	ALPHA | DEC_DIGIT;
fragment START_CHAR:	ALPHA_NUM | '_';
fragment NAME_CHAR:	START_CHAR | '\'' | '-' | '+' | '.' | '"' | '*' | '#';
//fragment ATM_NAME_CHAR:	ALPHA_NUM | '\'';
fragment SIMPLE_NAME:	START_CHAR NAME_CHAR*;

SPACE:			[ \t\r\n]+ -> skip;
ENCLOSE_COMMENT:	'{' (ENCLOSE_COMMENT | .)*? '}' -> channel(HIDDEN);
SECTION_COMMENT:	('#' | '!' | ';' | '\\' | '&' | '/' '/'+ | '*' '*'+ | '-' '-'+ | '+' '+'+ | '=' '='+ | R E M A R K S?) ' '* [\r\n]+ -> channel(HIDDEN);
LINE_COMMENT:		(';' | '\\' | '&' | '<' | '>' | '/' '/'+ | '*' '*'+ | '-' '-'+ | '+' '+'+ | '=' '='+ | R E M A R K S?) ~[\r\n]* -> channel(HIDDEN);

mode COMMENT_MODE;

Any_name:		~[ \t\r\n]+;

SPACE_CM:		[ \t]+ -> skip;
RETURN_CM:		[\r\n]+ -> mode(DEFAULT_MODE);

mode HBOND_MODE;

Atom1:			A T O M '1';
Atom2:			A T O M '2';
Residue1:		R E S I D U E '1';
Residue2:		R E S I D U E '2';

Equ_op_HB:		'=';

Integer_HB:		('+' | '-')? DECIMAL;
Simple_name_HB:		SIMPLE_NAME;

SPACE_HB:		[ \t]+ -> skip;
RETURN_HB:		[\r\n]+ -> mode(DEFAULT_MODE);

//SECTION_COMMENT_HB:	('#' | '!' | ';' | '\\' | '&' | '/' '/'+ | '*' '*'+ | '-' '-'+ | '+' '+'+ | '=' '='+ | R E M A R K S?) ' '* [\r\n]+ -> channel(HIDDEN);
LINE_COMMENT_HB:	('#' | '!' | ';' | '\\' | '&' | '<' | '>' | '/' '/'+ | '*' '*'+ | '-' '-'+ | '+' '+'+ | '=' '='+ | R E M A R K S?) ~[\r\n]* -> channel(HIDDEN);

mode PRINT_MODE;

Double_quote_string:	'"' ~["\r\n]* '"';

SPACE_PR:		[ \t]+ -> skip;
RETURN_PR:		[\r\n]+ -> mode(DEFAULT_MODE);

//SECTION_COMMENT_PR:	('#' | '!' | ';' | '\\' | '&' | '/' '/'+ | '*' '*'+ | '-' '-'+ | '+' '+'+ | '=' '='+ | R E M A R K S?) ' '* [\r\n]+ -> channel(HIDDEN);
LINE_COMMENT_PR:	('#' | '!' | ';' | '\\' | '&' | '<' | '>' | '/' '/'+ | '*' '*'+ | '-' '-'+ | '+' '+'+ | '=' '='+ | R E M A R K S?) ~[\r\n]* -> channel(HIDDEN);

mode VARIABLE_MODE;

Simple_name_VA:		SIMPLE_NAME;

SPACE_VA:		[ \t]+ -> skip;
RETURN_VA:		[\r\n]+ -> mode(DEFAULT_MODE);

//SECTION_COMMENT_VA:	('#' | '!' | ';' | '\\' | '&' | '/' '/'+ | '*' '*'+ | '-' '-'+ | '+' '+'+ | '=' '='+ | R E M A R K S?) ' '* [\r\n]+ -> channel(HIDDEN);
LINE_COMMENT_VA:	('#' | '!' | ';' | '\\' | '&' | '<' | '>' | '/' '/'+ | '*' '*'+ | '-' '-'+ | '+' '+'+ | '=' '='+ | R E M A R K S?) ~[\r\n]* -> channel(HIDDEN);

mode MAP_MODE;

Ambig_code_MP:		(DECIMAL '-' DECIMAL ':' DECIMAL | (ALPHA | '_') SIMPLE_NAME '.' DECIMAL);
Integer_MP:		('+' | '-')? DECIMAL;
Simple_name_MP:		SIMPLE_NAME;

Equ_op_MP:		'=';

SPACE_MP:		[ \t]+ -> skip;
RETURN_MP:		[\r\n] -> mode(DEFAULT_MODE);

//SECTION_COMMENT_MP:	('#' | '!' | ';' | '\\' | '&' | '/' '/'+ | '*' '*'+ | '-' '-'+ | '+' '+'+ | '=' '='+ | R E M A R K S?) ' '* [\r\n]+ -> channel(HIDDEN);
LINE_COMMENT_MP:	('#' | '!' | ';' | '\\' | '&' | '<' | '>' | '/' '/'+ | '*' '*'+ | '-' '-'+ | '+' '+'+ | '=' '='+ | R E M A R K S?) ~[\r\n]* -> channel(HIDDEN);

